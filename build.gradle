plugins {
  id 'java'
  id 'java-base'
  id 'java-library'
  id 'maven-publish'
}


group = "de.timesnake"
version = '1.3.1'
var projectId = 37

repositories {
  mavenLocal()
  mavenCentral()
  maven {
    url 'https://repo.papermc.io/repository/maven-public/'
  }
  maven {
    url "https://git.timesnake.de/api/v4/groups/7/-/packages/maven"
    name "TimeSnake"
    credentials {
      username = timesnakeUser
      password = timesnakePassword
    }
  }
}

dependencies {
  implementation "org.freemarker:freemarker:2.3.31"
  implementation 'commons-io:commons-io:2.11.0'

  compileOnly "de.timesnake:database-proxy:2.2.2"
  compileOnly "de.timesnake:database-api:2.2.2"

  compileOnly "de.timesnake:channel-proxy:3.0.6"
  compileOnly "de.timesnake:channel-api:3.0.6"

  compileOnly "de.timesnake:library-extension:1.2"
  compileOnly "de.timesnake:library-basic:1.2.2"
  compileOnly "de.timesnake:library-chat:1.1"

  compileOnly "com.moandjiezana.toml:toml4j:0.7.3-SNAPSHOT"
}

configurations.configureEach {
  resolutionStrategy.dependencySubstitution {
    if (project.parent != null) {
      substitute module("de.timesnake:database-proxy") using project(":database-proxy")
      substitute module("de.timesnake:database-api") using project(":database-api")
      substitute module("de.timesnake:channel-proxy") using project(":channel-proxy")
      substitute module("de.timesnake:channel-api") using project(":channel-api")
      substitute module("de.timesnake:library-extension") using project(":library-extension")
      substitute module("de.timesnake:library-basic") using project(":library-basic")
      substitute module("de.timesnake:library-chat") using project(":library-chat")
    }
  }
}

publishing {
  repositories {
    maven {
      url "https://git.timesnake.de/api/v4/projects/" + projectId + "/packages/maven"
      credentials(HttpHeaderCredentials) {
        name = "Job-Token"
        value = System.getenv("CI_JOB_TOKEN")
      }
      authentication {
        header(HttpHeaderAuthentication)
      }
    }
  }

  publications {
    mavenJava(MavenPublication) {
      from components.java
    }
  }
}

tasks.withType(JavaCompile).configureEach {
  options.encoding = 'UTF-8'
}

java {
  withSourcesJar()
}